1: <program> -> <moduleDeclarations> <otherModules> <driverModule> <otherModules> PROGRAMEND
2: <moduleDeclarations> -> <moduleDeclaration> <moduleDeclarations>
3: <moduleDeclarations> -> EPSILON
4: <moduleDeclaration> -> DECLARE MODULE ID SEMICOL
5: <otherModules> -> <module> <otherModules>
6: <otherModules> -> EPSILON
7: <driverModule> -> DRIVERDEF DRIVER PROGRAM DRIVERENDDEF <moduleDef>
8: <module> -> DEF MODULE ID ENDEF TAKES INPUT SQBO <input_plist> SQBC SEMICOL <ret> <moduleDef>
9: <ret> -> RETURNS SQBO <output_plist> SQBC SEMICOL
10: <ret> -> EPSILON
11: <input_plist> -> ID COLON <dataType> <iP0>
12: <iP0> -> COMMA ID COLON <dataType> <iP0>
13: <iP0> -> EPSILON
14: <output_plist> -> ID COLON <type> <oP0>
15: <oP0> -> COMMA ID COLON <type> <oP0>
16: <oP0> -> EPSILON
17: <dataType> -> INTEGER
18: <dataType> -> REAL
19: <dataType> -> BOOLEAN
20: <dataType> -> ARRAY SQBO <range_arr> SQBC OF <type>
21: <type> -> INTEGER
22: <type> -> REAL
23: <type> -> BOOLEAN
24: <moduleDef> -> START <statements> END
25: <statements> -> <statement> <statements>
26: <statements> -> EPSILON
27: <statement> -> <ioStmt>
28: <statement> -> <simpleStmt>
29: <statement> -> <declareStmt>
30: <statement> -> <conditionalStmt>
31: <statement> -> <iterativeStmt>
32: <ioStmt> -> GET_VALUE BO ID BC SEMICOL
33: <ioStmt> -> PRINT BO <var3> BC SEMICOL
34: <var3> -> <var>
35: <var3> -> <bools>
36: <var> -> ID <whichId>
37: <var> -> NUM
38: <var> -> RNUM
39: <whichId> -> SQBO <wI0>
40: <whichId> -> EPSILON
41: <wI0> -> <index_arr> SQBC
42: <simpleStmt> -> <assignmentStmt>
43: <simpleStmt> -> <moduleReuseStmt>
44: <assignmentStmt> -> ID <whichStmt>
45: <whichStmt> -> <lvalueIDStmt>
46: <whichStmt> -> <lvalueARRStmt>
47: <lvalueIDStmt> -> ASSIGNOP <expression> SEMICOL
48: <lvalueARRStmt> -> SQBO <element_index_with_expression> SQBC ASSIGNOP <expression> SEMICOL
49: <element_index_with_expression> -> <sign> <N_10>
50: <sign> -> PLUS
51: <sign> -> MINUS
52: <sign> -> EPSILON
53: <N_10> -> <arrExpr>
54: <N_10> -> BO <arrExpr> BC
55: <arrExpr> -> <arrTerm> <arr_N4>
56: <arr_N4> -> <op1> <arrTerm> <arr_N4>
57: <arr_N4> -> EPSILON
58: <arrTerm> -> <arrFactor> <arr_N5>
59: <arr_N5> -> <op2> <arrFactor> <arr_N5>
60: <arr_N5> -> EPSILON
61: <arrFactor> -> BO <arrExpr> BC
62: <arrFactor> -> <id_num>
63: <id_num> -> ID
64: <id_num> -> NUM
65: <arrFactor> -> <bools>
66: <moduleReuseStmt> -> SQBO <idList> SQBC ASSIGNOP USE MODULE ID WITH PARAMETERS <actual_para_list> SEMICOL
67: <moduleReuseStmt> -> USE MODULE ID WITH PARAMETERS <actual_para_list> SEMICOL
68: <actual_para_list> -> <aP00> <aP1>
69: <aP1> -> COMMA <aP00> <aP1>
70: <aP1> -> EPSILON
71: <aP00> -> <sign> <aP0>
72: <aP00> -> <bools>
73: <aP0> -> NUM
74: <aP0> -> RNUM
75: <aP0> -> ID <N_11>
76: <idList> -> ID <iL0>
77: <iL0> -> COMMA ID <iL0>
78: <iL0> -> EPSILON
79: <expression> -> <booleanExpr>
80: <booleanExpr> -> <arithmeticExpr> <BE00>
81: <booleanExpr> -> <bools> <BE0>
82: <BE00> -> <relationalOp> <arithmeticExpr> <BE0>
83: <BE00> -> <BE0>
84: <BE0> -> <logicalOp> <booleanExpr>
85: <BE0> -> EPSILON
86: <arithmeticExpr> -> <sign> <term> <AE0>
87: <AE0> -> <op1> <term> <AE0>
88: <AE0> -> EPSILON
89: <term> -> <factor> <T0>
90: <T0> -> <op2> <factor> <T0>
91: <T0> -> EPSILON
92: <op1> -> PLUS
93: <op1> -> MINUS
94: <op2> -> MUL
95: <op2> -> DIV
96: <bools> -> TRUE
97: <bools> -> FALSE
98: <factor> -> BO <expression> BC
99: <factor> -> NUM
100: <factor> -> RNUM
101: <factor> -> ID <N_11>
102: <N_11> -> SQBO <element_index_with_expression> SQBC
103: <N_11> -> EPSILON
104: <logicalOp> -> AND
105: <logicalOp> -> OR
106: <relationalOp> -> EQ
107: <relationalOp> -> LT
108: <relationalOp> -> LE
109: <relationalOp> -> GT
110: <relationalOp> -> GE
111: <relationalOp> -> NE
112: <declareStmt> -> DECLARE <idList> COLON <dataType> SEMICOL
113: <conditionalStmt> -> SWITCH BO ID BC START <caseStmts> <default> END
114: <caseStmts> -> <caseStmt> <caseStmts>
115: <caseStmts> -> EPSILON
116: <caseStmt> -> CASE <value> COLON <statements> BREAK SEMICOL
117: <value> -> NUM
118: <value> -> TRUE
119: <value> -> FALSE
120: <default> -> DEFAULT COLON <statements> BREAK SEMICOL
121: <default> -> EPSILON
122: <iterativeStmt> -> FOR BO ID IN <forrange> BC START <statements> END
123: <iterativeStmt> -> WHILE BO <booleanExpr> BC START <statements> END
124: <forrange> -> <sign> NUM RANGEOP <sign> NUM
125: <range_arr> -> <index_arr> RANGEOP <index_arr>
126: <index_arr> -> <sign> <new_index>
127: <new_index> -> NUM
128: <new_index> -> ID